# Maintainer: Arindam Chanda
pkgname=saw
pkgver=0.1.3
pkgrel=1
pkgdesc="Fast, multipurpose tool for AWS CloudWatch Logs"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
provides=('saw')
url="https://github.com/absolutarin/$pkgname"
license=('MIT')
makedepends=('go' 'git' 'dep')
source=("https://github.com/absolutarin/$pkgname/archive/v$pkgver.tar.gz")
md5sums=('a9daec2bee15e595e71424d720767d8b')

prepare() {
  mkdir -p "${srcdir}/go/src/github.com/absolutarin/"
  mv "${srcdir}/${pkgname}-${pkgver}" "${srcdir}/go/src/github.com/absolutarin/${pkgname}"
}

build() {
  export GOPATH="${srcdir}/go"
  export PATH="$PATH:$srcdir/go/bin"
  cd "${srcdir}/go/src/github.com/absolutarin/${pkgname}"
  dep ensure
  go build .
}

package() {
  cd "${srcdir}/go/src/github.com/absolutarin/${pkgname}"
  install -Dm755 saw "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
